# The model consists of a monovalent extracellular ligand, 
# a monovalent cell-surface receptor, a bivalent cytosolic adapter protein, 
# and a cytosolic kinase.  The receptor dimerizes through a 
# receptor-receptor interaction that depends on ligand binding.  
# The adapter binds the receptor and the kinase.  
# When two kinases are juxtaposed through binding to 
# receptor-associated adapter proteins, one of the kinases can 
# transphosphorylate the second kinase. 


begin parameters
/*! \desc Total amount of L */
L_tot 	1
/*! \desc Total amount of R */
R_tot   1
/*! \desc Total amount of A */
A_tot 	1
/*! \desc Total amount of K */
K_tot	1

/*! \desc Rate of pL 
 *  \type rate */
kpL 	0.1
/*! \desc Rate of mL 
 *  \type rate */
kmL 	0.1
/*! \desc Rate of pD 
 *  \type rate 
 *  \note kpD and kmD were divided by two from their values in to give the right symmetry factor
 */
kpD 	1.0
/*! \desc Rate of mD
 *  \type rate 
 *  \note kpD and kmD were divided by two from their values in to give the right symmetry factor
 */
kmD 	0.1
/*! \desc Rate of pA
 *  \type rate */
kpA 	0.1
/*! \desc Rate of mA 
 *  \type rate */
kmA 	0.1
/*! \desc Rate of pK 
 *  \type rate */
kpK 	0.1
/*! \desc Rate of mK 
 *  \type rate */
kmK 	0.1
/*! \desc Rate of pK 
 *  \type rate */
pK  	 1
/*! \desc Rate of pKs 
 *  \type rate */
pKs	10
/*! \desc Rate of dM
 *  \type rate */
dM       1
/*! \desc Rate of dC 
 *  \type rate */
dC	10
end parameters


begin seed species
/*! \desc Set to zero for equilibration	*/
L(r)	         0 

/*! \desc r binds to l of R */
R(l,r,a)         R_tot 

/*! \desc l binds to r of L
 *  \desc r binds to r of R
*/
A(r,k)	         1.0

/*! \desc r binds to a of R
 *  \desc k binds to a of K
 *  \desc a binds to k of A
 *  \desc Y is phosphorylation site that is either unphosphorylated (U) or phosphorylated (P 0)
*/
K(a,Y~U)         K_tot

end seed species

begin reaction rules
/*! \desc Ligand binding (L+R)
 *  \note specifying r in R here means that the r component must not be bound. 
          This prevents dissociation of ligand from R when R is in a dimer.
 */
L(r) + R(l,r) <-> L(r!1).R(l!1,r) kpL, kmL

/*! \desc  Aggregation (R+R)
 *  \note  R must be bound to ligand to dimerize.
*/
L(r!1).R(l!1,r) + L(r!1).R(l!1,r) <-> L(r!1).R(l!1,r!3).L(r!2).R(l!2,r!3) kpD,kmD

/*! \desc Receptor binding to adaptor (R+A)
 *  \note A and R can bind independent of whether 
          A is bound to K or whether R is in a dimer. */
A(r) + R(a) <-> A(r!1).R(a!1) kpA,kmA

/*! \desc Adaptor binding kinase 
    \note Doesn't depend on phosphorylation state of K or whether A is bound to
          receptor, i.e. binding rate is same whether A is on membrane (bound to
          R) or in cytosol.
*/
A(k) + K(a) <-> A(k!1).K(a!1) kpK,kmK

/*! \desc Kinase transphosphorylation by inactive kinase
 *  \note Rule doesn't specify how two K's are associated
*/
K(Y~U).K(Y~U) -> K(Y~U).K(Y~P) pK

/*! \desc Kinase transphosphorylation by active kinase
 *  \note Rule doesn't specify how two K's are associated
*/
K(Y~P).K(Y~U) -> K(Y~P).K(Y~P) pKs

/*! \desc Dephosphorylation of kinase in membrane complex
R(a!1).A(r!1,k!2).K(a!2,Y~P) -> R(a!1).A(r!1,k!2).K(a!2,Y~U) dM
*/

/*! \desc Dephosphorylation of kinase in cytosol */
K(a,Y~P) -> K(a,Y~U) dC

end reaction rules

begin observables
/*! \desc RecDim is useful to observe because... */
Molecules	RecDim	      R(r!+)

/*! \desc Rec_A is useful to observe because... */
Molecules	Rec_A	      R(a!1).A(r!1)

/*! \desc Rec_K is useful to observe because... */
Molecules	Rec_K	      R(a!1).A(r!1,k!2).K(a!2)

/*! \desc Rec_Kp is useful to observe because... */
Molecules	Rec_Kp	      R(a!1).A(r!1,k!2).K(a!2,Y~P)

/*! \desc RecDim_Kp is useful to observe because... */
Molecules	RecDim_Kp     R.R(a!1).A(r!1,k!2).K(a!2,Y~P)

/*! \desc L_total is useful to observe because... */
Molecules	L_total	      L

/*! \desc A_total is useful to observe because... */
Molecules	A_total       A

/*! \desc K_total is useful to observe because... */
Molecules	K_total       K

/*! \desc R_total is useful to observe because... */
Molecules	R_total	      R

end observables


## actions ##

generate_network({overwrite=>1});

# Equilibration
simulate_ode({t_end=>1000,n_steps=>10,atol=>1e-10,rtol=>1e-8,sparse=>1,steady_state=>1});
#writeSBML({});

# Kinetics
setConcentration("L(r)","L_total");
simulate_ode({t_end=>120,n_steps=>120,atol=>1e-10,rtol=>1e-8});


# Modified Kinetics, starts from end point of previous simulate_ode command
#setParameter("pKs",0);
#simulate_ode({suffix=>"kinetics2",t_end=>100,n_steps=>10,atol=>1e-10,rtol=>1e-8});
